plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.freefair.lombok' version '5.3.0'
}

group = 'net.nowtryz.utils'
version = '0.2.0-SNAPSHOT'
description = 'mcutils'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:deprecation"
}

ext {
    jdk = JavaVersion.current().majorVersion
    jdk_javadoc = "https://docs.oracle.com/javase/$jdk/docs/api/".toString()
    if (JavaVersion.current().isJava11Compatible()) {
        jdk_javadoc = "https://docs.oracle.com/en/java/javase/$jdk/docs/api/".toString()
    }

    isRelease = !version.toString().endsWith('-SNAPSHOT')
    artifactoryUser = System.getenv('ARTIFACTORY_USER')
    artifactoryPassword = System.getenv('ARTIFACTORY_PASSWORD')
}

repositories {
    mavenCentral()
    maven {
        name = 'spigot repo'
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }
    maven {
        name = 'sonatype repo'
        url = uri('https://oss.sonatype.org/content/groups/public/')
    }
    mavenLocal()
}

dependencies {
    api 'com.google.inject:guice:4.2.3'
    api 'com.google.inject.extensions:guice-assistedinject:4.2.3'
    api 'com.vdurmont:semver4j:3.1.0'
    api 'org.apache.commons:commons-lang3:3.10'
    api 'com.google.mug:mug:4.5'
    implementation 'org.reflections:reflections:0.9.12'
    compileOnly 'org.spigotmc:spigot:1.16.4-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:19.0.0'
}

javadoc.options {
    encoding = 'UTF-8'
    links = [
            jdk_javadoc,
            'https://google.github.io/guice/api-docs/latest/javadoc/',
            'https://javadoc.io/static/com.vdurmont/semver4j/3.1.0/'
    ]
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url = 'https://nowtryz.jfrog.io/artifactory/' + (isRelease ? 'mcutils-releases' : 'mcutils-snapshots')
            credentials {
                username = artifactoryUser
                password = artifactoryPassword
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Minecraft Utilities'
                description = 'Utilities to build bukkit plugins'
                url = 'https://github.com/nowtryz/mcutils'
                licenses {
                    license {
                        name = 'GNU General Public License version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        name = 'Nowtryz'
                        email = 'nowtryz.dev@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/nowtryz/mcutils.git'
                    developerConnection = 'scm:git:ssh://git@github.com:nowtryz/mcutils.git'
                    url = 'https://github.com/nowtryz/mcutils'
                }
            }
        }
    }
}
